buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.2.RELEASE'
	}
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'jacoco'
apply plugin: 'checkstyle' // used for source code formatting
apply plugin: 'pmd'		   // used for generally accepted best practices and designed well



version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

ext {
	guavaVersion = '19.0'
	jadiraVersion = '5.0.0.GA'
	swaggerVersion = '2.4.0' 
	jacocoVersion = '0.7.6.201602180812'
	checkstyleVersion = '7.6.1'
	pmdVersion = '5.5.5'
}

repositories {
	jcenter()
}

test {
    exclude '**/AbstractControllerTest.class'
    exclude '**/AbstractTest.class'
}
jacoco {
	toolVersion = jacocoVersion
	reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = true
        html.enabled = true
        html.destination = "${buildDir}/jacocoHtml"
    }
}
test.finalizedBy jacocoTestReport

checkstyle {
	toolVersion = checkstyleVersion
	config = rootProject.resources.text.fromFile('etc/checkstyle/rules.xml')
	
}
pmd {
	toolVersion = pmdVersion
	ruleSetConfig = rootProject.resources.text.fromFile('etc/pmd/ruleset.xml')
	ignoreFailures = true
}
springBoot {
	buildInfo()
}
dependencies {
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest'
	
	compile group: 'org.springframework', name: 'spring-context-support'
	
	compile group: 'joda-time', name: 'joda-time'
	compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda'
	compile group: 'com.google.guava', name: 'guava', version: guavaVersion
	//compile group: 'com.github.ben-manes.caffeine', name: 'caffeine'
	compile group: 'org.jadira.usertype', name: 'usertype.extended', version: jadiraVersion
	compile group: 'io.springfox', name: 'springfox-swagger2', version: swaggerVersion
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: swaggerVersion
	
	
	runtime group: 'org.hsqldb', name: 'hsqldb'
	runtime group: 'mysql', name: 'mysql-connector-java'
	
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

defaultTasks 'clean', 'build'
check.finalizedBy projectReport
projectReport.finalizedBy buildDashboard



